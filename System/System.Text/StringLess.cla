class StringLess : Less
{
    maide prusate Bool Init()
    {
        this.StringComp : share StringComp;
        return true;
    }
    
    field prusate IntLess CharLess { get { return data; } set { data : value; } }
    field prusate CharForm LiteCharForm { get { return data; } set { data : value; } }
    field prusate CharForm RiteCharForm { get { return data; } set { data : value; } }
    field precate StringComp StringComp { get { return data; } set { data : value; } }

    maide prusate Int Execute(var Any lite, var Any rite)
    {
        var StringComp comp;
        comp : this.StringComp;
        
        var String liteString;
        var String riteString;
        liteString : cast String(lite);
        riteString : cast String(rite);

        var Int liteCount;
        var Int riteCount;
        liteCount : comp.Count(liteString);
        riteCount : comp.Count(riteString);

        var IntLess charLess;
        charLess : this.CharLess;

        var CharForm liteCharForm;
        var CharForm riteCharForm;
        liteCharForm : this.LiteCharForm;
        riteCharForm : this.RiteCharForm;

        var Int count;
        count : liteCount;
        inf (riteCount < count)
        {
            count : riteCount;
        }

        var Int i;
        i : 0;
        while (i < count)
        {
            var Int na;
            var Int nb;
            na : comp.Char(liteString, i);
            nb : comp.Char(riteString, i);

            na : liteCharForm.Execute(na);
            nb : riteCharForm.Execute(nb);

            var Int oo;
            oo : charLess.Execute(na, nb);
            inf (!(oo = 0))
            {
                return oo;
            }

            i : i + 1;
        }

        return liteCount - riteCount;
    }
}