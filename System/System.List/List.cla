class List : Any
{
    maide prusate Bool Init()
    {
        base.Init();
        this.NodeRef : new ListNodeRef;
        this.NodeRef.Init();
        return true;
    }

    field prusate Int Count { get { return data; } set { data : value; } }
    field prusate Any FirstIndex
    {
        get
        {
            return this.First;
        }
        set
        {
        }
    }
    field prusate Any LastIndex
    {
        get
        {
            return this.Last;
        }
        set
        {
        }
    }
    field private ListNode First { get { return data; } set { data : value; } }
    field private ListNode Last { get { return data; } set { data : value; } }
    field private ListNodeRef NodeRef { get { return data; } set { data : value; } }

    maide prusate Any Add(var Any item)
    {
        var ListNode node;
        node : new ListNode;
        node.Init();
        node.Ref : this.NodeRef;
        node.Value : item;

        inf (!(this.Last = null))
        {
            node.Previous : this.Last;
            this.Last.Next : node;
        }

        inf (this.First = null)
        {
            this.First : node;
        }
        
        this.Last : node;

        this.Count : this.Count + 1;

        var Any a;
        a : node;
        return a;
    }

    maide prusate Any Ins(var Any index, var Any item)
    {
        inf (index = null)
        {
            return null;
        }

        var ListNode t;
        t : this.Node(index);
        inf (t = null)
        {
            return null;
        }

        var ListNode node;
        node : new ListNode;
        node.Init();
        node.Ref : this.NodeRef;
        node.Value : item;

        inf (this.First = t)
        {
            this.First : node;
        }

        inf (!(t.Previous = null))
        {
            t.Previous.Next : node;
            node.Previous : t.Previous;
        }

        node.Next : t;
        t.Previous : node;

        this.Count : this.Count + 1;

        var Any ret;
        ret : node;
        return ret;
    }

    maide prusate Bool Rem(var Any index)
    {
        inf (index = null)
        {
            return false;
        }
        var ListNode node;
        node : this.Node(index);
        inf (node = null)
        {
            return false;
        }

        inf (this.First = node)
        {
            this.First : node.Next;
        }

        inf (this.Last = node)
        {
            this.Last : node.Previous;
        }

        inf (!(node.Next = null))
        {
            node.Next.Previous : node.Previous;
        }

        inf (!(node.Previous = null))
        {
            node.Previous.Next : node.Next;
        }

        node.Ref : null;

        this.Count : this.Count - 1;
        return true;
    }

    maide prusate Bool Clear()
    {
        this.First : null;
        this.Last : null;

        this.Count : 0;

        this.NodeRef : new ListNodeRef;
        this.NodeRef.Init();
        return true;
    }

    maide prusate Bool Valid(var Any index)
    {
        inf (index = null)
        {
            return false;
        }
        var ListNode node;
        node : this.Node(index);
        inf (node = null)
        {
            return false;
        }
        return true;
    }

    maide prusate Any Get(var Any index)
    {
        inf (index = null)
        {
            return null;
        }
        var ListNode node;
        node : this.Node(index);
        inf (node = null)
        {
            return null;
        }

        var Any a;
        a : node.Value;
        return a;
    }

    maide prusate Bool Set(var Any index, var Any value)
    {
        inf (index = null)
        {
            return false;
        }
        var ListNode node;
        node : this.Node(index);
        inf (node = null)
        {
            return false;
        }
        node.Value : value;
        return true;
    }

    maide prusate Iter IterCreate()
    {
        var Iter a;
        a : new Iter;
        a.Init();
        return a;
    }

    maide prusate Bool IterSet(var Iter iter)
    {
        iter.CurrentNode : null;
        iter.Node : this.First;
        return true;
    }

    maide private ListNode Node(var Any index)
    {
        var ListNode node;
        node : cast ListNode(index);

        inf (node = null)
        {
            return null;
        }

        var Bool b;
        b : (node.Ref = this.NodeRef);
        inf (!b)
        {
            return null;
        }

        var ListNode a;
        a : node;
        return a;
    }
}